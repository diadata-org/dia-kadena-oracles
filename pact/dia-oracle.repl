(begin-tx "load modules")

  (env-data {
    'admin-keyset: { "keys": ["admin-key"], "pred": "keys-all" },
    'namespace-keyset: { "keys": [ ], "pred": "keys-all" },
    'upgrade: false
  })

  (define-namespace "free" (read-keyset 'namespace-keyset) (read-keyset 'namespace-keyset))

  (env-sigs [{ "key": "admin-key", "caps": [ ] }])
  (load "dia-oracle.pact")

  (typecheck "free.dia-oracle")
  (verify "free.dia-oracle")

(commit-tx)

(begin-tx "set value")
  (use free.dia-oracle)

  (env-sigs [{ "key": "admin-key", "caps": [ ] }])

  (let ((key "key")
        (timestamp (at "block-time" (chain-data)))
        (value 10.0))

    (expect "should store a value"
      true
      (set-value key timestamp value)
    )

    (expect "should emit an update event"
      [{
        "module-hash": (at "hash" (describe-module "free.dia-oracle")),
        "name": "free.dia-oracle.UPDATE",
        "params": [key timestamp value]
      }]
      (env-events true)
    )
  )

(commit-tx)

(begin-tx "set value validations")
  (use free.dia-oracle)

  (env-sigs [{ "key": "unknown-key", "caps": [ ] }])

  (expect-failure "should fail because ADMIN capability cannot be granted"
    "Keyset failure"
    (set-value "key" (at "block-time" (chain-data)) 10.0)
  )

(commit-tx)

(begin-tx "set multiple values")
  (use free.dia-oracle)

  (env-sigs [{ "key": "admin-key", "caps": [ ] }])

  (let* ((keys ["a" "b" "c"])
        (timestamps (make-list (length keys) (at "block-time" (chain-data))))
        (values (make-list (length keys) 10.0)))

    (expect "should store multiple values"
      (make-list (length keys) true)
      (set-multiple-values keys timestamps values)
    )

    (expect "should emit multiple update events"
      (map (lambda (ix)
        { "module-hash": (at "hash" (describe-module "free.dia-oracle")),
          "name": "free.dia-oracle.UPDATE",
          "params": [(at ix keys) (at ix timestamps) (at ix values)]
        }
      ) (enumerate 0 (- (length keys) 1)))
      (env-events true)
    )
  )

(commit-tx)

(begin-tx "set multiple values validations")
  (use free.dia-oracle)

  (expect-failure "should fail when list sizes are not equal"
    "Input lengths should be equal"
    (set-multiple-values ["a" "b"] [ ] [1.0])
  )

  (env-sigs [{ "key": "unknown-key", "caps": [ ] }])

  (expect-failure "should fail because ADMIN capability cannot be granted"
    "Keyset failure"
    (set-multiple-values [] [] [])
  )

(commit-tx)

(begin-tx "update value internal")
  (use free.dia-oracle)

  (env-sigs [{ "key": "admin-key", "caps": [ ] }])

  (expect-failure "should fail because STORAGE capability is missing"
    "not granted: (free.dia-oracle.STORAGE)"
    (update-value "key" UNIX_EPOCH 10.0)
  )

  (test-capability (STORAGE))

  (expect-failure "should fail when timestamp is negative"
    "Timestamp should be positive"
    (update-value "key" (add-time UNIX_EPOCH -1) 10.0)
  )

(commit-tx)

(begin-tx "get value")
  (use free.dia-oracle)

  (expect "should read the correct value"
    { "timestamp": (at "block-time" (chain-data)), "value": 10.0 }
    (get-value "key")
  )

  (expect "should return a default value when they key doesn't exist"
    { "timestamp": UNIX_EPOCH, "value": 0.0 }
    (get-value "unknown-key")
  )

(commit-tx)
